-- PASO 1: Crear la Base de Datos y el Schema
CREATE DATABASE Bd_Customers;
GO

USE Bd_Customers;
GO

CREATE SCHEMA VENTAS
GO


-- PASO 2: Crear las Tablas

-- 1. TABLA CLIENTES
-- Almacena la información del cliente.
--------------------------------------------------------------------------------
CREATE TABLE VENTAS.TBCLIENTES (
    FIIDCLIENTE     INT IDENTITY(1,1) PRIMARY KEY,
    FCNOMBRE        VARCHAR(100) NOT NULL,
    FCEMAIL         VARCHAR(100) NOT NULL UNIQUE, -- Restricción Unique para evitar duplicados
    FCTELEFONO      VARCHAR(20) NOT NULL,
    FDFECHAREGISTRO DATETIME DEFAULT GETDATE(),
    FBACTIVO        BIT DEFAULT 1
);
GO
-- 2. TABLA PRODUCTOS
-- Almacena la información de los productos.
CREATE TABLE VENTAS.TBPRODUCTOS (
    FIIDPRODUCTO    INT IDENTITY(1,1) PRIMARY KEY,
    FCSKU           VARCHAR(50) NOT NULL UNIQUE,
    FCNOMBRE        VARCHAR(100),
    FICANTIDAD      INT DEFAULT 0, 
    FDPRECIOBASE    DECIMAL(18,2),
    FBACTIVO        BIT DEFAULT 1
);

-- 3. TABLA ORDENES
-- Almacena la información de las ordenes.
CREATE TABLE VENTAS.TBORDENES (
    FIIDORDEN       INT IDENTITY(1000,1) PRIMARY KEY,
    FIIDCLIENTE     INT NOT NULL,
    FDSUBTOTAL      DECIMAL(18,2) NOT NULL,
    FDIVA           DECIMAL(18,2) NOT NULL, -- El cálculo del 16% se guarda aquí
    FDTOTAL         DECIMAL(18,2) NOT NULL,
    FDFECHAORDEN    DATETIME DEFAULT GETDATE()
   
    CONSTRAINT FK_ORDENES_CLIENTE FOREIGN KEY (FIIDCLIENTE) 
    REFERENCES VENTAS.TBCLIENTES(FIIDCLIENTE)
);
GO

-- 4. TABLA ORDENES ITEMS
-- Almacena la información de las ordenes i los items.
CREATE TABLE VENTAS.TBORDENITEMS (
    FIIDITEM        INT IDENTITY(100,1) PRIMARY KEY,
    FIIDORDEN       INT NOT NULL,
    FCSKU           VARCHAR(50) NOT NULL,
    FICANTIDAD      INT NOT NULL,
    FDPRECIOUNITARIO DECIMAL(18,2) NOT NULL, 
    FDIMPORTE       DECIMAL(18,2) NOT NULL, 
    
    CONSTRAINT FK_ITEMS_ORDEN FOREIGN KEY (FIIDORDEN) 
    REFERENCES VENTAS.TBORDENES(FIIDORDEN)
);
GO
-- 5. CREACION DE INDEX
CREATE INDEX IX_CLIENTES_EMAIL ON VENTAS.TBCLIENTES(FCEMAIL);
CREATE INDEX IX_ORDENES_CLIENTE ON VENTAS.TBORDENES(FIIDCLIENTE);
CREATE INDEX IX_ITEMS_ORDEN ON VENTAS.TBORDENITEMS(FIIDORDEN);
GO

-- 6. INGRESAR PRODUCTOS
INSERT INTO VENTAS.TBPRODUCTOS (FCSKU, FCNOMBRE, FICANTIDAD, FDPRECIOBASE)
VALUES 
('MOTO12345678', 'Italika 150Z Negra', 10, 28000.00),
('CASC1234567', 'Casco Rojo Certificado', 50, 850.00),
('CASC1234578', 'Casco Avenger', 50, 700.00),
('MOTO12881257', 'HERO 200 Azul', 50, 30000.00),
('ACEI55009988', 'Aceite Sintético 4T 1L', 200, 180.00),
('LUBR77001122', 'Lubricante Cadena Spray', 150, 120.00);
GO


-- Paso 3. CREACION DE LOS SP

--SP QUE REGISTRA CLIENTES

CREATE OR ALTER PROCEDURE VENTAS.SPAPIREGISTRACLIENTE
    @PACNOMBRE      VARCHAR(100),
    @PACEMAIL       VARCHAR(100),
    @PACTELEFONO    VARCHAR(20),
    @PAIIDCLIENTE   INT = 0 OUTPUT,  
    @PAIEXITO       INT = 0 OUTPUT,
    @PACMENSAJE     VARCHAR(256) = '' OUTPUT
AS
BEGIN
    SET NOCOUNT ON;
    
    -- CONSTANTES
    DECLARE @CSIVALORCERO INT = 0
           ,@CSIVALORUNO INT = 1
           ,@CSCMSGEXITO VARCHAR(100) = 'Cliente registrado exitosamente.'
           ,@CSCMSGDUPLICADO VARCHAR(100) = 'El correo electrónico ya se encuentra registrado.';

    SET @PAIIDCLIENTE = 0;
    SET @PAIEXITO = 0;
    SET @PACMENSAJE = '';

    BEGIN TRY
        -- 1. VALIDACIÓN PREVIA: Verificar si el correo ya existe
        IF EXISTS (SELECT 1 FROM VENTAS.TBCLIENTES WITH(NOLOCK) WHERE FCEMAIL = @PACEMAIL)
        BEGIN
            -- SI EXISTE CORREO RETORNAMOS ERROR
            SET @PAIEXITO = @CSIVALORCERO;
            SET @PACMENSAJE = @CSCMSGDUPLICADO;
            RETURN;
        END

        -- INSERCION
        INSERT INTO VENTAS.TBCLIENTES (FCNOMBRE, FCEMAIL, FCTELEFONO)
        VALUES (@PACNOMBRE, @PACEMAIL, @PACTELEFONO);

        -- SALIDA EXITOSA
        SET @PAIIDCLIENTE = SCOPE_IDENTITY();
        SET @PAIEXITO = @CSIVALORUNO;
        SET @PACMENSAJE = @CSCMSGEXITO;

    END TRY
    BEGIN CATCH
	-- MANEJO DE ERRORES
        SET @PAIEXITO = @CSIVALORCERO;
        SET @PACMENSAJE = 'Error al registrar: ' + ERROR_MESSAGE();
        SET @PAIIDCLIENTE = 0;
    END CATCH
END
GO

--SP QUE CONSULTA CLIENTES POR ID
CREATE OR ALTER PROCEDURE VENTAS.SPAPICONSULTACLIENTEPORID
   @PAIIDCLIENTE INT
AS
BEGIN
   SET NOCOUNT ON
   SELECT 
      FCNOMBRE,
      FCEMAIL,
      FCTELEFONO
   FROM VENTAS.TBCLIENTES
   WHERE FIIDCLIENTE = @PAIIDCLIENTE;

END
GO

--SP QUE CONSULTA DETALLE DE ORDEN
CREATE OR ALTER PROCEDURE VENTAS.SPAPICONSULTAORDENDETALLE
    @PAIIDORDEN INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        O.FIIDORDEN,
        O.FDFECHAORDEN,
        O.FDSUBTOTAL,
        O.FDIVA,
        O.FDTOTAL,

        C.FIIDCLIENTE,
        C.FCNOMBRE,
        C.FCEMAIL,
        C.FCTELEFONO,

        I.FCSKU,
        I.FICANTIDAD,
        I.FDPRECIOUNITARIO,
        I.FDIMPORTE

    FROM Bd_Customers.VENTAS.TBORDENES O
    INNER JOIN Bd_Customers.VENTAS.TBCLIENTES C 
        ON O.FIIDCLIENTE = C.FIIDCLIENTE
    INNER JOIN Bd_Customers.VENTAS.TBORDENITEMS I 
        ON O.FIIDORDEN = I.FIIDORDEN
    WHERE 
        O.FIIDORDEN = @PAIIDORDEN;

END
GO

--SP QUE INSERTA UNA ORDEN
CREATE OR ALTER PROCEDURE VENTAS.SPAPIREGISTRAORDEN
    @PAIIDCLIENTE   INT,
    @PACJSONITEMS   NVARCHAR(MAX),
    @PADSUBTOTAL    DECIMAL(18,2),
    @PADIVA         DECIMAL(18,2),
    @PADTOTAL       DECIMAL(18,2),
    @PAIIDORDEN     INT = 0 OUTPUT,
    @PAIEXITO       INT = 0 OUTPUT,
    @PACMENSAJE     VARCHAR(256) = '' OUTPUT
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @CSIVALORCERO INT = 0
           ,@CSIVALORUNO  INT = 1
           ,@VCSKUINVALIDO VARCHAR(50) = NULL

    -- Tabla temporal para los items
    DECLARE @TBLITEMS TABLE (
        SKU             VARCHAR(50),
        CANTIDAD        INT,
        PRECIOUNITARIO  DECIMAL(18,2),
        IMPORTE         DECIMAL(18,2)
    )

    BEGIN TRY
        -- 1. VALIDAR CLIENTE
        IF NOT EXISTS (SELECT 1 FROM VENTAS.TBCLIENTES WITH(NOLOCK) WHERE FIIDCLIENTE = @PAIIDCLIENTE)
        BEGIN
            SET @PAIEXITO = @CSIVALORCERO;
            SET @PACMENSAJE = 'El Cliente especificado no existe.';
            RETURN;
        END

        -- 2. DESERIALIZAR JSON
        INSERT INTO @TBLITEMS (SKU, CANTIDAD, PRECIOUNITARIO, IMPORTE)
        SELECT 
            sku, cantidad, precioUnitario, (cantidad * precioUnitario)
        FROM OPENJSON(@PACJSONITEMS)
        WITH (
            sku             VARCHAR(50)   '$.sku',
            cantidad        INT           '$.cantidad',
            precioUnitario  DECIMAL(18,2) '$.precioUnitario'
        );

        IF NOT EXISTS (SELECT 1 FROM @TBLITEMS)
        BEGIN
            SET @PAIEXITO = @CSIVALORCERO;
            SET @PACMENSAJE = 'La lista de productos está vacía o el formato JSON es incorrecto.';
            RETURN;
        END

        -- 3. VALIDACIÓN: PRODUCTOS EXISTENTES
        SELECT TOP 1 @VCSKUINVALIDO = T.SKU
        FROM @TBLITEMS T
        LEFT JOIN VENTAS.TBPRODUCTOS P WITH(NOLOCK) ON T.SKU = P.FCSKU
        WHERE P.FIIDPRODUCTO IS NULL;

        IF @VCSKUINVALIDO IS NOT NULL
        BEGIN
            SET @PAIEXITO = @CSIVALORCERO;
            SET @PACMENSAJE = 'El producto con SKU [' + @VCSKUINVALIDO + '] no existe en el catálogo.';
            RETURN; 
        END

        -- 4. TRANSACCIÓN DE GUARDADO
        BEGIN TRANSACTION

            INSERT INTO VENTAS.TBORDENES 
                (FIIDCLIENTE, FDSUBTOTAL, FDIVA, FDTOTAL)
            VALUES 
                (@PAIIDCLIENTE, @PADSUBTOTAL, @PADIVA, @PADTOTAL);

            SET @PAIIDORDEN = SCOPE_IDENTITY();

            INSERT INTO VENTAS.TBORDENITEMS 
                (FIIDORDEN, FCSKU, FICANTIDAD, FDPRECIOUNITARIO, FDIMPORTE)
            SELECT 
                @PAIIDORDEN, SKU, CANTIDAD, PRECIOUNITARIO, IMPORTE
            FROM @TBLITEMS;

        COMMIT TRANSACTION

        SET @PAIEXITO = @CSIVALORUNO;
        SET @PACMENSAJE = 'Orden registrada exitosamente.';

    END TRY
    BEGIN CATCH
        IF @@TRANCOUNT > 0 ROLLBACK TRANSACTION;
        SET @PAIEXITO = @CSIVALORCERO;
        SET @PAIIDORDEN = 0;
        SET @PACMENSAJE = 'Error interno: ' + ERROR_MESSAGE();
    END CATCH
END
GO

-- PASO 4: Crear el USUARIO y dar PERMISOS

USE master;
GO

-- 1. Crear el LOGIN 
IF NOT EXISTS (SELECT name FROM sys.server_principals WHERE name = 'USRAPICUSTOMERS')
BEGIN
    CREATE LOGIN USRAPICUSTOMERS 
    WITH PASSWORD = 'UsuarioCustomer', 
    DEFAULT_DATABASE = Bd_Customers, 
    CHECK_POLICY = OFF;
END
GO

-- 2. Cambiar a tu Base de Datos
USE Bd_Customers;
GO

-- 3. Crear el USUARIO de base de datos
IF NOT EXISTS (SELECT name FROM sys.database_principals WHERE name = 'USRAPICUSTOMERS')
BEGIN
    CREATE USER USRAPICUSTOMERS FOR LOGIN USRAPICUSTOMERS;
END
GO

-- 4. ASIGNAR PERMISOS

-- Conectarse a la BD 
GRANT CONNECT TO USRAPICUSTOMERS;

-- EJECUTAR CUALQUIER STORED PROCEDURE

GRANT EXECUTE TO USRAPICUSTOMERS;

GO